
group 'com.rundeck'

ext.rundeckPluginVersion = '1.2'
ext.pluginClassNames='com.rundeck.plugin.ExampleAuditEventListener'
ext.pluginName = 'Example Audit Listener plugin'
ext.pluginDescription = 'This plugin is an example auditing events listener.'

project.version = "1.0.0"

apply plugin: 'groovy'
apply plugin: 'java'

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

configurations {
    pluginLibs

    compile {
        extendsFrom pluginLibs
    }
}


dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.3.11'
    compile 'org.rundeck:rundeck-core:3.1.0-SNAPSHOT'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile "org.codehaus.groovy:groovy-all:2.3.7"
    testCompile "org.spockframework:spock-core:0.7-groovy-2.0"
    testCompile "cglib:cglib-nodep:2.2.2"
    testCompile 'org.objenesis:objenesis:1.4'
}


task copyToLib(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.pluginLibs
}

jar {
    from "$buildDir/output"
    manifest {
        def libList = configurations.pluginLibs.collect{'lib/' + it.name}.join(' ')
        attributes 'Rundeck-Plugin-Name' : pluginName
        attributes 'Rundeck-Plugin-Description' : pluginDescription
        attributes 'Rundeck-Plugin-Rundeck-Compatibility-Version': '3.1.0+'
        attributes 'Rundeck-Plugin-Tags': 'java,audit handler'
        attributes 'Rundeck-Plugin-License': 'Apache 2.0'
//        attributes 'Rundeck-Plugin-Source-Link': 'https://github.com/rundeck-plugins/git-plugin'
        attributes 'Rundeck-Plugin-Target-Host-Compatibility': 'all'
        attributes 'Rundeck-Plugin-Author': 'Rundeck, Inc.'
        attributes 'Rundeck-Plugin-Classnames': pluginClassNames
        attributes 'Rundeck-Plugin-File-Version': project.version
        attributes 'Rundeck-Plugin-Version': rundeckPluginVersion
        attributes 'Rundeck-Plugin-Archive': 'true'
        attributes 'Rundeck-Plugin-Libs': "${libList}"
    }
    dependsOn(copyToLib)
}
